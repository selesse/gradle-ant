apply plugin: 'java'
apply plugin: 'jetty'
apply plugin: 'war'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.slf4j:slf4j-log4j12:1.6.6'

    testCompile 'junit:junit:4.11'
}

def buildProperties = new Properties()
file("some.properties").withInputStream{
    buildProperties.load(it);
}

task createConfigs() {
    group 'Configuration'
    description 'Generates all configuration files'

    FileTree sources = fileTree(projectDir).include("**/*.in")
                                           .exclude("classes/", "bin/", "build/")

    sources.each { File source ->
        File newFile = new File(source.parentFile, source.name.replaceAll("\\.in", ""))
        def createTaskName = 'createConfig' + newFile.name.capitalize()
        def deleteTaskName = 'deleteConfig' + newFile.name.capitalize()

        task(createTaskName, type: Copy) {
            group 'Configuration'
            description 'Generates configuration file from "' +
                projectDir.toURI().relativize(source.toURI()) + '"'

            inputs.file source
            outputs.file newFile

            from source
            into source.parentFile
            rename '(.+)*.in', '$1'
            filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: buildProperties)
        }

        task(deleteTaskName, type: Delete) {
            delete newFile
        }

        createConfigs.dependsOn(createTaskName)
        clean.dependsOn(deleteTaskName)
    }
}

processResources.dependsOn createConfigs
